grammar com.sandy.xtext.JoveNotes with org.eclipse.xtext.common.Terminals

generate joveNotes "http://www.sandy.com/xtext/JoveNotes"

JoveNotes :
    ( processingHints = ProcessingHints )
    ( chapterDetails = ChapterDetails )
    ( notesElements += NotesElement )*
;

ProcessingHints:
    {ProcessingHints}
    (skipGeneration             = '@skip_generation')?
    (skipGenerationInProduction = '@skip_generation_in_production')?
;

ChapterDetails:
    ( exerciseBank = '@exercise_bank' )?
    'subject'       subjectName   = STRING
    'chapterNumber' chapterNumber = INT '.' subChapterNumber = INT
    'chapterName'   chapterName   = STRING
    ( scriptBody = ScriptBody )?
;

NotesElement: (
    WordMeaning    | 
    QuestionAnswer | 
    FIB            |
    Definition     |
    Character      |
    TeacherNote    |
    Matching       |
    Event          |
    TrueFalse      |
    ChemEquation   |
    ChemCompound   |
    Spellbee       |
    ImageLabel     |
    Equation       |
    RefToContext   |
    MultiChoice    |
    Exercise       |
    VoiceToText    |
    CompilerBreak  |
    ChapterSection )
    ( script = Script )?
;

WordMeaning:
    '@wm' (hideFromView = 'hide')?
          word    = STRING
          meaning = STRING
;

QuestionAnswer:
    '@qa' ( hideFromView = 'hide' )?
            question     = STRING
          ( answerParts += STRING )+
          ( cmap         = CMap   )?
;

FIB:
    '@fib' (hideFromView = 'hide')?
            question  = STRING
            (answers  += STRING)+
;

Definition:
    '@definition' (hideFromView = 'hide')?
                  term       = STRING
                  definition = STRING
                  ( cmap     = CMap )?
;

Character:
    '@character' (hideFromView = 'hide')?
                 character   = STRING
                 estimate    = STRING
                 ( cmap      = CMap )?
;

TeacherNote:
    '@tn' (hideFromView = 'hide')?
          (caption = STRING)?
           note = STRING
          (cmap = CMap)?
;

Matching:
    '@match' (hideFromView = 'hide')?
             (skipReverseQuestion = 'skip_reverse_question')?
             (question = STRING)? '{'
        ( pairs += MatchPair )+
        ( mcqConfig = MatchMCQConfig )?
    '}'
;

MatchPair: matchQuestion = STRING '=' matchAnswer = STRING ;

MatchMCQConfig:
    '@mcq_config' '{'
          '@forwardCaption'   forwardCaption   = STRING
        ( '@reverseCaption'   reverseCaption   = STRING )?
        ( '@numOptionsToShow' numOptionsToShow = INT    )?
        ( '@numOptionsPerRow' numOptionsPerRow = INT    )?
    '}'
;

Event: '@event' (hideFromView = 'hide')?
                event = STRING time = STRING
;

TrueFalse: 
    '@true_false' (hideFromView = 'hide')?
                   statement = STRING 
                   truthValue = BOOL
                   (justification = STRING)?
;

ChemEquation :
    '@chem_equation' (hideFromView = 'hide')?
                     (description = STRING)? '{'
        reactants = STRING '>' (produces = STRING)? '>' products = STRING 
    '}'
;

ChemCompound :
    '@chem_compound' (hideFromView = 'hide')?
                     symbol      = STRING
                     chemicalName= STRING
                     (commonName  = STRING)?
;

Spellbee :
    '@spellbee' (hideFromView = 'hide')?
                word = STRING
;

ImageLabel:
    '@image_label' (hideFromView = 'hide')?
                   (caption = STRING)? '{'
        'imageName' imageName = STRING
        ( hotspots += HotSpot )( ',' hotspots += HotSpot )*
    '}'
;

HotSpot:
    x = INT ',' y = INT '=' label = STRING
;

Equation :
    '@equation' (hideFromView = 'hide')?
                 equation = STRING '{'
                (description = STRING)?
                'where'
                ( symbols += EqSymbol )( ',' symbols += EqSymbol )*
    '}'
;

EqSymbol:
    symbol = STRING '=' description = STRING
;

RefToContext:
    '@rtc' (hideFromView = 'hide')? '{'
        'context' context = STRING
        ( rtcElement += RTCElement )+
    '}'
;

RTCElement: 
    WordMeaning    | 
    QuestionAnswer | 
    FIB            |
    Definition     |
    Character      |
    TeacherNote    |
    Matching       |
    Event          |
    TrueFalse      |
    ChemEquation   |
    ChemCompound   |
    ImageLabel     |
    Equation       |
    MultiChoice 
;

MultiChoice:
    '@multi_choice' ( hideFromView = 'hide' )? question = STRING '{'
        '@options' '{'
            ( options += Option )( ',' options += Option )*
        '}'
        ( '@numOptionsToShow' numOptionsToShow = INT )?
        ( '@numOptionsPerRow' numOptionsPerRow = INT )?
        ( '@explanation' explanation = STRING )?
    '}'
;

Exercise:
    '@exercise' ( hideFromView = 'hide' )?
                ( 'marks' '=' marks = INT )
                ( question = STRING )
                ( 'hints' '{' ( hints += STRING )+ '}' )?
                ( 'answer' answer = STRING )
;

VoiceToText:
    '@voice2text' ( hideFromView = 'hide' )?
                  ( clipName = STRING )
                  ( text = STRING )
;

CompilerBreak:
    {CompilerBreak}
    '@compiler_break'
;

ChapterSection:
    '@section' ( sectionName = STRING )
;

Option:
    optionValue = STRING
    (correctOption = 'correct' )?
;

CMap: 'cmap' '{' content = STRING '}' ;

Script:
    'script_expressions' '{'
        ( evalVars += EvalVar )( ',' evalVars += EvalVar )*
    '}'
    ( scriptBody = ScriptBody )?
;

EvalVar:
    varName = STRING '=' varExpression = STRING
;

ScriptBody:
    'script' '{'
        script = STRING
    '}'
;

terminal BOOL : 'true' | 'false' ;
