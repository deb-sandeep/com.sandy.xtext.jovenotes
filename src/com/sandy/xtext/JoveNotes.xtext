grammar com.sandy.xtext.JoveNotes with org.eclipse.xtext.common.Terminals

generate joveNotes "http://www.sandy.com/xtext/JoveNotes"

JoveNotes :
    ( processingHints = ProcessingHints )?
    ( chapterDetails = ChapterDetails )
    ( notesElements += NotesElement )*
;

ProcessingHints:
    skipGeneration = '@skip_generation'
;

ChapterDetails:
    ( testPaper = '@test_paper' )?
    'subject'       subjectName   = STRING
    'chapterNumber' chapterNumber = INT '.' subChapterNumber = INT
    'chapterName'   chapterName   = STRING
;

NotesElement:
    WordMeaning    | 
    QuestionAnswer | 
    FIB            |
    Definition     |
    Character      |
    TeacherNote    |
    Matching       |
    Event          |
    TrueFalse      |
    ChemEquation   |
    ChemCompound   |
    Spellbee       |
    ImageLabel     |
    Equation       |
    RefToContext   
;

WordMeaning:
    '@wm' (hideFromView = 'hide')?
          word    = STRING
          meaning = STRING
;

QuestionAnswer:
    '@qa' (hideFromView = 'hide')?
          question = STRING
          answer   = STRING
          ( cmap = CMap )?
;

FIB:
    '@fib' (hideFromView = 'hide')?
            question  = STRING
            (answers  += STRING)+
;

Definition:
    '@definition' (hideFromView = 'hide')?
                  term       = STRING
                  definition = STRING
                  ( cmap     = CMap )?
;

Character:
    '@character' (hideFromView = 'hide')?
                 character   = STRING
                 estimate    = STRING
                 ( cmap      = CMap )?
;

TeacherNote:
    '@tn' (hideFromView = 'hide')?
           note = STRING
          (cmap = CMap)?
;

Matching:
    '@match' (hideFromView = 'hide')?
             (skipReverseQuestion = 'skip_reverse_question')?
             (question = STRING)? '{'
        ( pairs += MatchPair )+
    '}'
;

MatchPair: matchQuestion = STRING '=' matchAnswer = STRING ;

Event: '@event' (hideFromView = 'hide')?
                event = STRING time = STRING
;

TrueFalse: 
    '@true_false' (hideFromView = 'hide')?
                   statement = STRING 
                   truthValue = BOOL
                   (justification = STRING)?
;

ChemEquation :
    '@chem_equation' (hideFromView = 'hide')?
                     (description = STRING)? '{'
        reactants = STRING '>' (produces = STRING)? '>' products = STRING 
    '}'
;

ChemCompound :
    '@chem_compound' (hideFromView = 'hide')?
                     symbol      = STRING
                     chemicalName= STRING
                     (commonName  = STRING)?
;

Spellbee :
    '@spellbee' (hideFromView = 'hide')?
                word = STRING
;

ImageLabel:
    '@image_label' (hideFromView = 'hide')?
                   (caption = STRING)? '{'
        'imageName' imageName = STRING
        ( hotspots += HotSpot )( ',' hotspots += HotSpot )*
    '}'
;

HotSpot:
    x = INT ',' y = INT '=' label = STRING
;

Equation :
    '@equation' (hideFromView = 'hide')?
                 equation = STRING '{'
                (description = STRING)?
                'where'
                ( symbols += EqSymbol )( ',' symbols += EqSymbol )*
    '}'
;

EqSymbol:
    symbol = STRING '=' description = STRING
;

RefToContext:
    '@rtc' (hideFromView = 'hide')? '{'
        'context' context = STRING
        ( questions += QuestionAnswer )+
    '}'
;

CMap: 'cmap' '{' content = STRING '}' ;

terminal BOOL : 'true' | 'false' ;
